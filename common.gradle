// This file is to be applied to every project.
configurations {
    testArtifacts.extendsFrom testRuntime
}

apply plugin: 'java'
apply plugin: "jacoco"
apply plugin: 'maven'
apply plugin: 'maven-publish'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

dependencies {
    // Adding dependencies here will add the dependencies to each subproject.
    testCompile group: 'junit', name: 'junit', version: project.ext.'libraryVersion.junit.junit'
    testCompile group: 'org.hamcrest', name: 'hamcrest-junit', version: project.ext.'libraryVersion.hamcrest.hamcrest'
    testCompile group: 'org.mockito', name: 'mockito-core', version: project.ext.'libraryVersion.mockito.mockito'
}

test {
    reports {
        junitXml.enabled = true
        html.enabled = true
    }               
}

jacoco {
    toolVersion = project.ext.'gradlePluginVersion.jacoco.toolVersion'
}

String mavenArtifactId = name
group = project.ext.mavenGroupId
version = project.ext.mavenVersion
// Append suffix '-SNAPSHOT' if it is a snapshot release...
if ( !project.hasProperty( 'typeOfRelease' ) || project.ext.typeOfRelease.equalsIgnoreCase( 'SNAPSHOT' ) ) {
    version += '-SNAPSHOT'
}

task sourcesJar( type: Jar, dependsOn: classes, description: 'Creates a jar from the source files.' ) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar( type: Jar, dependsOn: 'javadoc', description: 'Creates a jar from the JavaDoc files.' ) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task testJar(type: Jar) {
    classifier "test"
    from sourceSets.test.output
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
    
    testArtifacts testJar
}

publishing {
    publications {
        mavenJava( MavenPublication ) {
            from components.java

            artifact sourcesJar
            artifact javadocJar
        }
    }
}

configure( install.repositories.mavenInstaller ) {
    pom.project {
        groupId = mavenGroupId
        artifactId = mavenArtifactId
        version = mavenVersion
    }
}

task createFolders( description: 'Creates the source folders if they do not exist.' ) doLast {
    sourceSets*.allSource*.srcDirs*.each { File srcDir ->
        if ( !srcDir.isDirectory() ) {
            println "Creating source folder: ${srcDir}"
            srcDir.mkdirs()
        }
    }
}
