// This file is to be applied to every project.
configurations {
    testArtifacts.extendsFrom testRuntime
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

apply plugin: 'java'
apply plugin: "jacoco"
apply plugin: 'maven'
apply plugin: 'maven-publish'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    maven {
        url "${repository_contextUrl}/${repository_libsRepositoryName}"
        credentials {
            username = "${repository_user}"
            password = "${repository_password}"
        
        }
    }
    maven {
        url "${repository_contextUrl}/${repository_remoteRepositoryName}"
        credentials {
            username = "${repository_user}"
            password = "${repository_password}"
        
        }
    }
    mavenCentral()
    jcenter()
}

dependencies {
    // OpenClover dependency for coverage analysis.
    clover 'org.openclover:clover:4.2.0'
    
    // Adding dependencies here will add the dependencies to each subproject.
    testCompile group: 'junit', name: 'junit', version: project.ext.'libraryVersion.junit.junit'
    testCompile group: 'org.hamcrest', name: 'hamcrest-junit', version: project.ext.'libraryVersion.hamcrest.hamcrest'
    testCompile group: 'org.mockito', name: 'mockito-core', version: project.ext.'libraryVersion.mockito.mockito'
}

test {
    reports {
        junitXml.enabled = true
        html.enabled = true
    }
    jacoco {
        append=true
        destinationFile = file( "${rootProject.buildDir}/jacoco/jacocoTest.exec" ) 
    }
}

jacoco {
    toolVersion = project.ext.'gradlePluginVersion.jacoco.toolVersion'
}

jacocoTestReport {
    additionalSourceDirs = files(sourceSets.main.allSource.srcDirs)
    sourceDirectories = files(sourceSets.main.allSource.srcDirs)
    classDirectories =  files(sourceSets.main.output)
    reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = false
    }
}

clover {
    classesBackupDir = file("${buildDir}/classes-backup")
    
    //additionalSourceSet {
    //    srcDirs = project(':test-utils').sourceSets.main.allSource.srcDirs
    //    srcDirs += project(':times-model-api').sourceSets.main.allSource.srcDirs
    //    classesDir = project(':test-utils').sourceSets.main.output.classesDir
    //    classesDir += project(':times-model-api').sourceSets.main.output.classesDir
    //}
    //additionalTestSourceSet {
    //    srcDirs = project(':test-utils').sourceSets.test.allSource.srcDirs
    //    srcDirs += project(':times-model-api').sourceSets.test.allSource.srcDirs
    //    classesDir = project(':test-utils').sourceSets.test.output.classesDir
    //    classesDir += project(':times-model-api').sourceSets.test.output.classesDir
    //}
    
    compiler {
        encoding = 'UTF-8'
    }
    
    report {
        html = true
        json = true
        pdf = true
    }
}

String mavenArtifactId = name
group = project.ext.mavenGroupId
version = project.ext.mavenVersion
// Append suffix '-SNAPSHOT' if it is a snapshot release...
if ( !project.hasProperty( 'typeOfRelease' ) || project.ext.typeOfRelease.equalsIgnoreCase( 'SNAPSHOT' ) ) {
    version += '-SNAPSHOT'
}

task sourcesJar( type: Jar, dependsOn: classes, description: 'Creates a jar from the source files.' ) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar( type: Jar, dependsOn: 'javadoc', description: 'Creates a jar from the JavaDoc files.' ) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task testJar(type: Jar) {
    classifier "test"
    from sourceSets.test.output
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
    
    testArtifacts testJar
}

publishing {
    def repoKey = ''
    
    if ( !project.hasProperty( 'typeOfRelease' ) || project.ext.typeOfRelease.equalsIgnoreCase( 'SNAPSHOT' ) ) {
        repoKey = 'snapshot'
    }
    else if ( project.ext.typeOfRelease.equalsIgnoreCase( 'RELEASE' ) ) {
        repoKey = 'release'
    }
    
    repositories {
        
        maven {
            name repoKey
            credentials {
                username repository_user
                password repository_password
            }
            if ( !project.hasProperty( 'typeOfRelease' ) || project.ext.typeOfRelease.equalsIgnoreCase( 'SNAPSHOT' ) ) {
                url "${repository_contextUrl}/${repository_snapshotRepositoryName}"
            }
            else if ( project.ext.typeOfRelease.equalsIgnoreCase( 'RELEASE' ) ) {
                url "${repository_contextUrl}/${repository_releaseRepositoryName}"
            }
        }
    }
    
    publications {
        mavenJava( MavenPublication ) {
            from components.java

            artifact sourcesJar
            artifact javadocJar
        }
    }
}

configure( install.repositories.mavenInstaller ) {
    pom.project {
        groupId = mavenGroupId
        artifactId = mavenArtifactId
        version = mavenVersion
    }
}

task createFolders( description: 'Creates the source folders if they do not exist.' ) doLast {
    sourceSets*.allSource*.srcDirs*.each { File srcDir ->
        if ( !srcDir.isDirectory() ) {
            println "Creating source folder: ${srcDir}"
            srcDir.mkdirs()
        }
    }
}
